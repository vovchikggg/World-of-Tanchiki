class Game {
    field Player player;  
    field Objects map;

    constructor Game new(){
        var Level levels; 
        let levels = Level.new();
        let map = levels.getFirstLevel();
        do map.setMap();
        do map.drowAllObjects();
        let player = map.getPlayer();
        return this;
    }

    method void dispose(){
        do player.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void MoveKeyboard(char key){
        if (key = 131) { // Up
            do player.setDirection(1);
            do player.movePlayer();
        }

        if (key = 133) { // Down
            do player.setDirection(2);
            do player.movePlayer();
        }

        if (key = 130) { //Left
            do player.setDirection(3);
            do player.movePlayer();
        }

        if (key = 132) { //Right
            do player.setDirection(4);
            do player.movePlayer();
        }
        return;
    }

    method void run(){
        var char key;
        var boolean exit;
        var Bullet newBullet;
        var int time;
        let time = 0;
        let exit = false;

        while (~exit){
            let key = Keyboard.keyPressed();

            if (key = 81) // Q - выйти
            {
                let exit = true;
            }

            if ((key = 32) & ( (time - ((time / 1000) * 1000)) = 0 )) // Space - пуля
            {
                let newBullet = Bullet.new(player, map);
                do map.addNewBullet(newBullet);
            }

            do MoveKeyboard(key);

            do map.killObjects();
            do map.clearBullets();
            do map.moveBullets();
            do map.drowAllObjects();

            let time = time + 1;
        }
        return;
    }
}