class Game {
    field Objects map;
    field Player player;
    field Base base;
    field BotsList botsList;
    field BricksList bricksList;
    field BulletsList bulletsList;
    field ExitGame exitGame;
    field int levelNumber;
    field Level level;
    field CurrentLevel currentLevel;

    constructor Game new(ExitGame _exitGame, Level _level, CurrentLevel _currentLevel){
        let level = _level;
        let currentLevel = _currentLevel;
        let levelNumber = _currentLevel.getLevel();

        if (levelNumber = 1)
        {
            let map = level.getFirstLevel();
        }
        if (levelNumber = 2)
        {
            let map = level.getSecondLevel();
        }

        let player = map.getPlayer();
        let base = map.getBase();
        do map.setMap();
        do map.drowAllObjects();
        let exitGame = _exitGame;

        return this;
    }

    method void MoveKeyboard(char key){
        if (key = 131) { // Up
            do player.setDirection(1);
            do player.movePlayer();
        }

        if (key = 133) { // Down
            do player.setDirection(2);
            do player.movePlayer();
        }

        if (key = 130) { //Left
            do player.setDirection(3);
            do player.movePlayer();
        }

        if (key = 132) { //Right
            do player.setDirection(4);
            do player.movePlayer();
        }
        return;
    }

    method void run(){
        var char key;
        var boolean exitLevel;
        var int playerTime;
        var int botsTime;

        let playerTime = 0;
        let botsTime = 0;
        let exitLevel = false;

        while (~exitLevel){
            let key = Keyboard.keyPressed();
            if (key = 49) // 1 Level (Нажать на 1)
            {
                do currentLevel.setLevel(1);
                let exitLevel = true;
            }

            if (key = 50) // 2 Level (Нажать на 2)
            {
                do currentLevel.setLevel(2);
                let exitLevel = true;
            }

            if (key = 81 | (base.getAlive() = false)) // Q - выйти
            {
                let exitLevel = true;
                do exitGame.setExitGame(true);
            }

            if (key = 32) // Space - пуля
            {   
                if (player.getAlive() = true)
                {
                    if (playerTime > player.getTimeBetweenAtacks())
                    {
                        do player.Atack();
                        let playerTime = 0;
                    }
                }
            }

            if (player.getAlive() = true)
            {
                do MoveKeyboard(key);
            }
            
            if (botsTime > 25)
            {
                do botsList.botsAtack();
                let botsTime = 0;
            }

            let botsList = map.getBotsList();
            let bricksList = map.getBricksList();
            let bulletsList = map.getBulletsList();

            do botsList.moveBots();
            do map.removeDeadBulletsFromList();
            do bulletsList.clearBullets();
            do bulletsList.moveBullets();
            do map.killObjectsWithBullet();
            do map.drowAllObjects();

            let playerTime = playerTime + 1;
            let botsTime = botsTime + 1;
        }
        return;
    }

    method void dispose()
    {
        do Screen.setColor(false);
        do Screen.drawRectangle(0,0,511,255);

        do Memory.deAlloc(this);
        return;
    }
}