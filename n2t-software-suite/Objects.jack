class Objects
{
    field Player player;
    field Base base;
    field BotsList botsList;
    field BricksList bricksList;
    field BulletsList bulletsList;

    field BotsList currentBot;
    field BotsList tempBot;
    field BotsList prevBot;

    field BricksList currentBrick;
    field BricksList tempBrick;
    field BricksList prevBrick;

    field BulletsList currentBullet;
    field BulletsList tempBullet;
    field BulletsList prevBullet;

    field Bot bot;
    field Brick brick;
    field Bullet bullet;

    constructor Objects new()
    {
        let player = null;
        let base = null;
        let botsList = null;
        let bricksList = null;
        let bulletsList = null;
        return this;
    }

    method List getPlayer()
    {
        return player;
    }
    method void setPlayer(Player _player)
    {
        let player = _player;
        return;
    }

    method List getBase()
    {
        return base;
    }
    method void setBase(Base _base)
    {
        let base = _base;
        return;
    }

    method BotsList getBotsList()
    {
        return botsList;
    }
    method void addNewBot(Bot _bot) {
        let prevBot = botsList;
        let botsList = BotsList.new(_bot, botsList);
        if (~(prevBot = null))
        {
            do prevBot.setPrev(botsList);
        }

        return;
    }

    method BricksList getBricksList()
    {
        return bricksList;
    }
    method void addNewBrick(Brick _brick) {
        let prevBrick = bricksList;
        let bricksList = BricksList.new(_brick, bricksList);
        if (~(prevBrick = null))
        {
            do prevBrick.setPrev(bricksList);
        }

        return;
    }

    method List getBulletsList()
    {
        return bulletsList;
    }
    method void addNewBullet(Bullet _bullet) {
        let prevBullet = bulletsList;
        let bulletsList = BulletsList.new(_bullet, bulletsList);
        if (~(prevBullet = null))
        {
            do prevBullet.setPrev(bulletsList);
        }

        return;
    }

    method void setMap()
    {
        if (~(player = null))
        {
            do player.setMap(this);
        }
        if (~(base = null))
        {
            do base.setMap(this);
        }
        if (~(botsList = null))
        {
            do botsList.setMap(this);
        }

        return;
    }

    method void drowAllObjects(Objects map)
    {
        if (~(player = null))
        {
            do player.drowPlayer();
        }
        if (~(base = null))
        {
            do base.drowBase();
        }
        if (~(botsList = null))
        {
            do botsList.drowBots(map);
        }
        if (~(bricksList = null))
        {
            do bricksList.drowBricks();
        }
        if (~(bulletsList = null))
        {
            do bulletsList.drowBullets();
        }

        return;
    }

    method void killObjectsWithBullet()
    {
        let currentBullet = bulletsList;
        while (~(currentBullet = null))
        {
            let bullet = currentBullet.getData();
            if (bullet.getAlive() = true)
            {
                if (~(player = null))
                {
                    do player.killPlayerWithBullet(bullet);
                }
                if (~(base = null))
                {
                    do base.killBaseWithBullet(bullet);
                }
                if (~(botsList = null))
                {
                    do botsList.killBotsWithBullet(bullet);
                }
                if (~(bricksList = null))
                {
                    do bricksList.killBricksWithBullet(bullet);
                }
                if (~(bulletsList = null))
                {
                    do bulletsList.killBulletsWithBullet(bullet);
                }
            }
            let currentBullet = currentBullet.getNext();
        }

        return;
    }

    method void removeDeadObjectsFromLists()
    {
        if (~(botsList = null))
        {
            do botsList.removeDeadBotsFromList();
        }
        if (~(bricksList = null))
        {
            do bricksList.removeDeadBricksFromList();
        }
        if (~(bulletsList = null))
        {
            do bulletsList.removeDeadBulletsFromList();
        }

        return;
    }

    method void dispose()
    {
        do disposeAllObjects();
        do Memory.deAlloc(this);
        return;
    }

    method void disposeAllObjects()
    {
        do player.dispose();

        do base.dispose();

        let currentBot = botsList;
        while (~(currentBot = null))
        {
            let tempBot = currentBot.getNext();
            do currentBot.dispose();
            let currentBot = tempBot;
        }

        let currentBrick = bricksList;
        while (~(currentBrick = null))
        {
            let tempBrick = currentBrick.getNext();
            do currentBrick.dispose();
            let currentBrick = tempBrick;
        }

        let currentBullet = bulletsList;
        while (~(currentBullet = null))
        {
            let tempBullet = currentBullet.getNext();
            do currentBullet.dispose();
            let currentBullet = tempBullet;
        }

        return;
    }
}